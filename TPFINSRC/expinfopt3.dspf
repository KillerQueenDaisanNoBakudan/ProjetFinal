      *%METADATA                                                       *
      * %TEXT DSPF Exploitation info centre                            *
      *%EMETADATA                                                      *
     A          R FMTENR                    SFL
     A            PR_CODE   R        O  9  8REFFLD(INFOCF1/PR_CODE FORMATION3/I-
     A                                      NFOCENTRE)
     A                                      EDTCDE(Z)
     A  55                                  COLOR(RED)
     A            PR_NOM    R        O  9 21REFFLD(INFOCF1/PR_NOM FORMATION3/IN-
     A                                      FOCENTRE)
     A  55                                  COLOR(RED)
     A            NBCEPAGE  R        O  9 73REFFLD(INFOCF1/NBCEPAGE FORMATION3/-
     A                                      INFOCENTRE)
     A                                      EDTCDE(3)
     A  55                                  COLOR(RED)
     A            OPT            1   I  9  3VALUES('2' '3' '4' '5')
     A  55                                  DSPATR(PR)
     A  55                                  COLOR(RED)
     A          R FMTCTL                    SFLCTL(FMTENR)
     A                                      SFLDSPCTL
     A                                      SFLDSP
     A                                      SFLPAG(10)
     A                                      SFLSIZ(11)
     A                                      CA12(12)
     A                                      CA06(06)
     A  34                                  SFLEND(*MORE)
     A N34                                  ROLLUP(38)
     A                                      OVERLAY
     A  30                                  SFLCLR
     A                                      PROTECT
     A            HAUTPAGE       5  0H      SFLSCROLL
     A            RANG           4  0H      SFLRCDNBR
     A                                  8  3'Opt'
     A                                      COLOR(BLU)
     A                                  8 13'Code'
     A                                      COLOR(BLU)
     A                                  8 21'Nom du producteur'
     A                                      COLOR(BLU)
     A                                  8 69'Nb cépage'
     A                                      COLOR(BLU)
     A                                  5  3'2 = Modifier'
     A                                      COLOR(RED)
     A                                  5 16'3 = Copier'
     A                                      COLOR(RED)
     A                                  5 27'4 = Supprimer'
     A                                      COLOR(RED)
     A                                  5 42'5 = Afficher'
     A                                      COLOR(RED)
     A                                  1 23'GESTION DES ENREGISTREMENTS'
     A                                      COLOR(BLU)
     A                                  3  3'Choisissez votre option puis faite-
     A                                      s entrée'
     A          R TOUCHES
     A                                 23 17'F12 = Annuler'
     A                                      COLOR(BLU)
     A                                 23  4'F6 = Créer'
     A                                      COLOR(BLU)
     A  32                             21  4'L''enregistrement a été copié'
     A          R DETAILS                   WINDOW(7 1 12 62)
     A                                      CA12(12)
     A                                      CF09(09)
     A                                 11  1'F12 = Retour'
     A                                      COLOR(BLU)
     A N70                              1 13'Details d''un producteur :'
     A                                      COLOR(TRQ)
     A  70                              1 13'Mise à jour d''un producteur :'
     A                                      COLOR(TRQ)
     A                                  3  1'Nom :'
     A                                  4  1'Téléphone : '
     A                                  5  1'Appellation :'
     A                                  6  1'Nombre de vins :'
     A                                  7  1'En cave :'
     A                                  8  1'Cépage le plus utilisé :'
     A                                  9  1'Nombre de cépage différent :'
     A            PR_NOM    R        B  3  8REFFLD(INFOCF1/PR_NOM FORMATION3/IN-
     A                                      FOCENTRE)
     A N70                                  DSPATR(PR)
     A            PR_TEL    R        B  4 15REFFLD(INFOCF1/PR_TEL FORMATION3/IN-
     A                                      FOCENTRE)
     A N70                                  DSPATR(PR)
     A            NBRVINS   R        O  6 18REFFLD(INFOCF1/NBRVINS FORMATION3/I-
     A                                      NFOCENTRE)
     A                                      EDTCDE(3)
     A            EN_CAVE   R        O  7 11REFFLD(INFOCF1/EN_CAVE FORMATION3/I-
     A                                      NFOCENTRE)
     A            CEPAGE    R        O  8 26REFFLD(INFOCF1/CEPAGE FORMATION3/IN-
     A                                      FOCENTRE)
     A            NBCEPAGE  R        O  9 30REFFLD(INFOCF1/NBCEPAGE FORMATION3/-
     A                                      INFOCENTRE)
     A                                      EDTCDE(3)
     A  70                             11 15'F9 = Valider'
     A                                      COLOR(BLU)
     A            APPEL1        40   O  5 15
     A            PR_CODE   R        O  1 44REFFLD(INFOCF1/PR_CODE FORMATION3/I-
     A                                      NFOCENTRE)
     A                                      EDTCDE(3)
     A                                      COLOR(TRQ)
     A          R DELETE                    WINDOW(7 1 6 62)
     A                                      CA12(12)
     A                                  5  2'F12 = Retour'
     A                                      COLOR(BLU)
     A                                  1 15'SUPPRIMER LE PRODUCTEUR ?'
     A                                      COLOR(RED)
     A                                  3  1'Nom :'
     A            PR_NOM    R        O  3  8REFFLD(INFOCF1/PR_NOM FORMATION3/IN-
     A                                      FOCENTRE)
     A            PR_CODE   R        O  2  8REFFLD(INFOCF1/PR_CODE FORMATION3/I-
     A                                      NFOCENTRE)
     A                                      EDTCDE(3)
     A                                  5 15'Entrée = Supprimer'
     A                                      COLOR(RED)
     A                                  2  1'Code :'
     A          R CREATE                    WINDOW(7 1 12 70)
     A                                      CA12(12)
     A                                  3  1'Nom :'
     A                                  4  1'Téléphone :'
     A                                  5  1'Appellation :'
     A                                  6  1'Nombre de vins :'
     A                                  7  1'En cave :'
     A                                  8  1'Cepage le plus utilisé :'
     A                                  9  1'Nombre de cépage différents :'
     A                                 11  1'F12 = Annuler'
     A                                      COLOR(BLU)
     A                                 11 19'Entrée = Valider'
     A                                  1 20'CRÉER UN NOUVEAU PRODUCTEUR'
     A                                      COLOR(TRQ)
     A            PR_NOM    R        I  3  9REFFLD(INFOCF1/PR_NOM FORMATION3/IN-
     A                                      FOCENTRE)
     A                                      CHECK(LC)
     A            PR_TEL    R        I  4 14REFFLD(INFOCF1/PR_TEL FORMATION3/IN-
     A                                      FOCENTRE)
     A            APPEL2        40   I  5 17
     A                                      CHECK(LC)
     A            NBRVINS   R     N  I  6 20REFFLD(INFOCF1/NBRVINS FORMATION3/I-
     A                                      NFOCENTRE)
     A            EN_CAVE   R        I  7 13REFFLD(INFOCF1/EN_CAVE FORMATION3/I-
     A                                      NFOCENTRE)
     A            CEPAGE    R        I  8 27REFFLD(INFOCF1/CEPAGE FORMATION3/IN-
     A                                      FOCENTRE)
     A                                      CHECK(LC)
     A            NBCEPAGE  R     N  I  9 33REFFLD(INFOCF1/NBCEPAGE FORMATION3/-
     A                                      INFOCENTRE)
